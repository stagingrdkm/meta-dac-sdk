# We have a conf and classes directory, add to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have recipes-* directories, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb \
            ${LAYERDIR}/recipes-*/*/*.bbappend"

BBFILE_COLLECTIONS += "meta-dac"
BBFILE_PATTERN_meta-dac = "^${LAYERDIR}/"
BBFILE_PRIORITY_meta-dac = "6"

LAYERDEPENDS_meta-dac = "core"
LAYERSERIES_COMPAT_meta-dac = "dunfell"

DISTRO_FEATURES_remove = " x11"
DISTRO_FEATURES_remove = " kde"
DISTRO_FEATURES_append = " egl"
DISTRO_FEATURES_append = " opengles2"
DISTRO_FEATURES_append = " enable_icrypto_openssl"
DISTRO_FEATURES_append = " netflix_1080p_graphics"
DISTRO_FEATURES_append = " cobalt_rialto"
DISTRO_FEATURES_remove = " bluetooth"

PACKAGECONFIG_remove_pn-cairo = " opengl"
PACKAGECONFIG_remove_pn-qtbase = " gbm"

PACKAGECONFIG_remove_pn-gstreamer1.0-plugins-bad = "vulkan"

DISTRO_FEATURES_append = " cleanup_gfx"
PREFERRED_PROVIDER_virtual/egl = "${@bb.utils.contains('DISTRO_FEATURES', 'cleanup_gfx', 'libglvnd', 'mesa', d)}"
PREFERRED_PROVIDER_virtual/libgles1 = "${@bb.utils.contains('DISTRO_FEATURES', 'cleanup_gfx', 'libglvnd', 'mesa', d)}"
PREFERRED_PROVIDER_virtual/libgles2 = "${@bb.utils.contains('DISTRO_FEATURES', 'cleanup_gfx', 'libglvnd', 'mesa', d)}"
PREFERRED_PROVIDER_virtual/libgl = "${@bb.utils.contains('DISTRO_FEATURES', 'cleanup_gfx', 'libglvnd', 'mesa', d)}"
PREFERRED_PROVIDER_virtual/mesa = "${@bb.utils.contains('DISTRO_FEATURES', 'cleanup_gfx', 'libglvnd', 'mesa', d)}"
PREFERRED_PROVIDER_virtual/cobalt-evergreen = "${@bb.utils.contains('DISTRO_FEATURES', 'no_pixel_buffer_surfaces', 'cobalt-evergreen-src', 'cobalt-evergreen-pbt', d)}"

PREFERRED_PROVIDER_virtual/wpebackend = "wpebackend-rdk"
PREFERRED_VERSION_libepoxy = "1.5.3"

RDK_GIT_PROTOCOL = "https"
CMF_GIT_PROTOCOL = "https"
CMF_GIT_BRANCH = "rdk-next"
CMF_GIT_ROOT = "git://code.rdkcentral.com/r"
CMF_GITHUB_MASTER_BRANCH ?= "master"
CMF_GITHUB_ROOT ?= "git://github.com/rdkcentral"
RDK_RELEASE ?= "1.99"
LICENSE_FLAGS_WHITELIST += "commercial_fdk-aac"

DISABLE_STATIC_pn-curl-netflix = ""
SECURITY_STRINGFORMAT_pn-netflix = ""

# Build Thunder R4? For now R2 still seems to suffice in DAC apps
#DISTRO_FEATURES_append = " wpe_r4"

# Enable Thunder R4_4
#DISTRO_FEATURES_append = " wpe_r4_4"

# cobalt-24 needs gstreamer1_18 which in turn needs dunfell
# uncomment to enable cobalt-24 version, the default is cobalt23
#DISTRO_FEATURES_append = " cobalt-24 gstreamer1_18 dunfell"

# uncomment to enable cobalt-25 version, the default is cobalt23
#DISTRO_FEATURES_append = " cobalt-25 gstreamer1_18 dunfell cobalt_enable_evergreen_lite"

# default we use libcobalt, uncomment below to use libloaderapp and evergreen lite
#DISTRO_FEATURES_append = " cobalt_enable_evergreen_lite"

# Enable the distro to generate Cobalt-25 OCI image for devices that do not support pixel buffer surfaces
#DISTRO_FEATURES_append = " no_pixel_buffer_surfaces"

#Enable rialto recipes from meta-rdk-video
#DISTRO_FEATURES_append = " enable_rialto"

WPEFRAMEWORK_VERSION = "${@bb.utils.contains('DISTRO_FEATURES', 'wpe_r4_4', '4.4%' , bb.utils.contains('DISTRO_FEATURES', 'wpe_r4', '4.2%', '2.1%', d), d)}"
PREFERRED_VERSION_wpeframework = "${WPEFRAMEWORK_VERSION}"
PREFERRED_VERSION_wpeframework-clientlibraries = "${WPEFRAMEWORK_VERSION}"
PREFERRED_VERSION_wpeframework-interfaces = "${WPEFRAMEWORK_VERSION}"
PREFERRED_VERSION_wpeframework-tools-native = "${WPEFRAMEWORK_VERSION}"
EXTRA_VERSIONS_PATH ?= "${TMPDIR}/versions"

BBMASK += "/meta-rdk/"
BBMASK += "/meta-cmf/(?!recipes-extended|recipes-devtools)"
BBMASK += "/meta-cmf/recipes-extended/(?!rpcserver)"
BBMASK += "/meta-cmf/recipes-devtools/(?!protobuf)"

# Netflix dependencies from rdk
# mask all unnecessary recipes from meta-rdk-video and meta-rdk-ext
BBMASK += "/meta-rdk-video/(?!recipes-extended|recipes-devtools)"
BBMASK += "${@bb.utils.contains('DISTRO_FEATURES', 'enable_rialto', '/meta-rdk-video/recipes-extended/(?!gstreamer-netflix-platform|wpe-framework|rialto)', '/meta-rdk-video/recipes-extended/(?!gstreamer-netflix-platform|wpe-framework)', d)}"
BBMASK += "${@bb.utils.contains('DISTRO_FEATURES', 'enable_rialto', 'recipes-core/rialto/', 'recipes-extended/rialto/', d)}"
BBMASK += "/meta-rdk-video/recipes-devtools/(?!gn)"

BBMASK += "/meta-rdk-ext/(?!recipes-devtools|recipes-extended|recipes-multimedia)"
BBMASK += "/meta-rdk-ext/recipes-extended/(?!graphite2|curl-netflix)"
BBMASK += "/meta-rdk-ext/recipes-devtools/(?!dwarfutils)"
# cobalt-24 needs gstreamer 1.18 inside /meta-rdk-ext/recipes-multimedia/gstreamer
# in other cases we use gstreamer 1.16 in meta-openembedded
BBMASK += "${@bb.utils.contains('DISTRO_FEATURES', 'cobalt-24', '/meta-rdk-ext/recipes-multimedia/(?!gstreamer)', '/meta-rdk-ext/recipes-multimedia/', d)}"


BBMASK += "/meta-rdk-netflix/recipes-extended/netflix-thunder-plugin"
